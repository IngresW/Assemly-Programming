# Assignment 5  工资计算



## 多个源程序的汇编

为了提高程序的可读性和复用性，汇编项目通常会将不同功能模块划分到多个源文件中。`salary.asm` 包含主逻辑代码，负责处理计算`table`的数据，而 `util.asm` 包含显示功能的工具函数。

在`salary.asm`文件中使用了`util.asm`中的工具函数，需要做如下声明

````
EXTRN PRINT_TAB: FAR, PRINT_NEWLINE: FAR, PRINT_NUMBER: FAR
````

在`util.asm`中，需要使用`PUBLIC`将函数导出

```
PUBLIC PRINT_TAB, PRINT_NEWLINE, PRINT_NUMBER
```

使用如下指令进行编译连接，得到可执行文件

```
masm salary.asm        // 得到 salary.obj
masm util.asm          // 得到 util.obj
link salary util       // 连接 salary.obj 和 util.obj 得到 salary.exe
```



## File 1. `salary.asm`

```
data segment
    years db '1975','1976','1977','1978','1979','1980','1981','1982','1983','1984','1985','1986','1987','1988','1989','1990','1991','1992','1993','1994','1995'
    revenues dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514,345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000
    employees dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226,11542,14430,15257,17800
data ends

table segment
    db 21 dup('year summ ne ?? ')
table ends

stack segment
    db 32 dup(0)
stack ends

code segment
    ASSUME CS: code, DS: data, ES: table, SS: stack
    EXTRN PRINT_TAB: FAR, PRINT_NEWLINE: FAR, PRINT_NUMBER: FAR
main PROC FAR
    MOV AX, data
    MOV DS, AX
    MOV AX, table
    MOV ES, AX
; 处理年份数据
    MOV SI, OFFSET years
    MOV DI, 0
    MOV CX, 21  ; 21年数据
copy_year:
    MOVSB
    MOVSB
    MOVSB
    MOVSB
    ADD DI, 12                  ; 每次跳过12个空格
    LOOP copy_year
; 处理收入数据
    MOV SI, OFFSET revenues
    MOV DI, 5                   ; 表中收入的起始偏移量
    MOV CX, 21
copy_revenues:
    MOV AX, [SI]
    MOV DX, [SI + 2]
    MOV ES:[DI], AX
    MOV ES:[DI + 2], DX
    ADD DI, 16
    ADD SI, 4
    LOOP copy_revenues
; 处理员工数据
    MOV SI, OFFSET employees
    MOV DI, 10                  ; 表中员工数量的起始偏移量
    MOV CX, 21
copy_employees:
    MOV AX, [SI]
    MOV ES:[DI], AX
    ADD DI, 16
    ADD SI, 2
    LOOP copy_employees
; 计算平均收入
    MOV SI, OFFSET revenues
    MOV DI, OFFSET employees
    MOV BX, 13
    MOV CX, 21
calculate:
    PUSH BX
    ; 收入数据
    MOV AX, [SI]
    MOV DX, [SI + 2]
    ADD SI, 4
    ; 员工数据
    MOV BX, [DI]
    ADD DI, 2
    ; 计算平均收入
    DIV BX
    POP BX
    PUSH DI
    ; 将平均收入存储到表中
    MOV DI, BX
    MOV ES:[DI], AX
    ADD DI, 16
    MOV BX, DI
    POP DI
    LOOP calculate
; 打印输出
    MOV AX, table
    MOV DS, AX
    MOV BX, 21
    MOV SI, 0
PRINT_TABLE:
    CALL PRINT_TAB
    MOV CX, 4                   ; 打印年份的长度
    PRINT_YEAR:
        MOV DL, [SI]                ; 逐个字符显示年份
        MOV AH, 02H
        INT 21H
        INC SI
        LOOP PRINT_YEAR
    ; 打印制表符
    CALL PRINT_TAB
    CALL PRINT_TAB
    CALL PRINT_TAB
    ; 打印收入
    INC SI
    MOV AX, [SI]
    ADD SI, 2
    MOV DX, [SI]
    ADD SI, 2
    CALL PRINT_NUMBER
    CALL PRINT_TAB
    CALL PRINT_TAB
    ; 打印员工数量
    INC SI
    MOV AX, [SI]
    MOV DX, 0
    ADD SI, 2
    CALL PRINT_NUMBER
    CALL PRINT_TAB
    CALL PRINT_TAB
    ; 打印平均收入
    INC SI
    MOV AX, [SI]
    MOV DX, 0
    ADD SI, 2
    CALL PRINT_NUMBER
    INC SI
    ; 换行
    CALL PRINT_NEWLINE
  
    DEC BX
    JNZ PRINT_TABLE

END_PROC:
    MOV AX, 4C00H
    INT 21H

main ENDP
code ends
END main
```

### 功能

1. **导入数据段的数据**：程序通过循环结构，将设定好的年份、工资、人数依次存入`table`。
2. **计算平均工资**：根据工资、人数，依次计算每年的平均工资。

### 实现逻辑

1. **数据段初始化**：设置数据段和表段的指针，以便后续操作。
2. **数据复制**：
   - 通过循环逐个复制年份、收入和员工数量至表中，确保每项数据正确存储。
3. **计算平均收入**：
   - 通过循环遍历每年的收入和员工数量，计算平均收入，并将结果存储到表中。
4. **数据输出**：
   - 使用外部打印函数，逐行打印年份、收入、员工数量和计算出的平均收入，确保输出格式整齐。



## File 2. `util.asm`

```
CODESEG SEGMENT
    ASSUME CS: CODESEG
    PUBLIC PRINT_TAB, PRINT_NEWLINE, PRINT_NUMBER
buffer_end DB 21 DUP(0)        ; 缓冲区
PRINT_TAB PROC FAR
    PUSH DX
    PUSH AX
    MOV DL, ' '
    MOV AH, 02H
    INT 21H
    INT 21H
    INT 21H
    INT 21H
    POP AX
    POP DX
    RET
PRINT_TAB ENDP
PRINT_NEWLINE PROC FAR
    PUSH DX
    PUSH AX
    MOV DL, 13
    MOV AH, 02H
    INT 21H
    MOV DL, 10
    MOV AH, 02H
    INT 21H
    POP AX
    POP DX
    RET
PRINT_NEWLINE ENDP
PRINT_NUMBER PROC FAR
    PUSH CX
    PUSH BX
    PUSH SI
    PUSH DI
    XOR SI, SI
    XOR DI, DI
    MOV DI, OFFSET buffer_end
CONVERT_LOOP:
    PUSH AX
    XOR AX, AX
    MOV AX, DX
    XOR DX, DX
    XOR BX, BX
    XOR CX, CX
    MOV CX, 10
    DIV CX
    MOV BX, AX
    POP AX
    DIV CX
    PUSH BX
    ADD DL, '0'
    DEC DI
    MOV [DI], DL
    INC SI
    POP DX
    CMP DX, 0
    JNE CONVERT_LOOP
    CMP AX, 0
    JNE CONVERT_LOOP
    MOV BX, 10
    SUB BX, SI
PRINT_START:
    MOV CX, SI
    PRINT_LOOP:
        MOV DL, [DI]
        MOV AH, 02H
        INT 21H
        INC DI
        LOOP PRINT_LOOP   
PRINT_SPACE_LOOP:
    CMP BX, 0                     ; 判断是否需要显示空格
    JLE PRINT_END
    MOV DL, ' '
    MOV AH, 02H
    INT 21H
    DEC BX
    JMP PRINT_SPACE_LOOP
PRINT_END:
    POP DI
    POP SI
    POP BX
    POP CX
    RET
PRINT_NUMBER ENDP
CODESEG ENDS
END
```

### 实现逻辑：

1. **公共函数声明**：使用 `PUBLIC` 指令声明了三个公共函数 `PRINT_TAB`、`PRINT_NEWLINE` 和 `PRINT_NUMBER`，可供其他模块调用。
2. **缓冲区**：定义了一个缓冲区 `buffer_end`，用于临时存储 `PRINT_NUMBER` 数字字符。
3. **打印制表符 (`PRINT_TAB`)**：
   - 该过程在屏幕上输出四个空格字符。
4. **打印换行 (`PRINT_NEWLINE`)**：
   - 该过程输出回车符和换行符，以便移动到下一行。
5. **打印数字 (`PRINT_NUMBER`)**：
   - 该过程实现将数字转换为字符并输出到屏幕。
   - 使用循环将数字逐位取出，通过除法和取余操作得到每一位数字，并按字符方式存入缓冲区。
   - 在输出时，首先打印数字，然后根据需要输出空格，以使格式整齐。